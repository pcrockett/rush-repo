#!/usr/bin/env nu

# TODO: support xdg standard env variables
let STATE_DIR = $"($env.HOME)/.local/state/tw"
let TEMPLATE_FILE = "template.toml"

# Taskwarrior wrapper that adds my own custom functionality
def main [
  ...args: string       # args to pass directly to `task`
] {
  ^task ...$args
}

# Add a task
def "main add" [...args: string] {
  let now = date now | date to-timezone UTC
  let template = (get-template $now)
  if ($template.add.expire > $now) {
    ^task add ...$template.add.args ...$args
  } else {
    ^task add ...$args
  }
}

# Interactively edit task notes with your $EDITOR
def "main notes" [
  task: string      # Task ID
] {
  edit $task "notes"
}

# Define a parameter template to include in all `add` commands
def "main template add" [
  --expire(-e) = 10min   # how long the template should remain in effect
  --clear                # clear the current template
  ...args: string
] {
  if $clear {
    error make (not-implemented)
  }

  let now = ((date now) + $expire) | date to-timezone UTC
  let config = {
    add: {
      expire: ($now | format date %+)
      args: $args
    }
  }
  mkdir $STATE_DIR
  $config | save --force $"($STATE_DIR)/($TEMPLATE_FILE)"
  ignore
}

# Interactively edit task attributes with your $EDITOR
def edit [
  task: string       # Task ID
  attribute: string  # Name of the attribute to edit
] {
  let temp_dir = mktemp --directory
  let cleanup = {
    cd -;
    rm --recursive --force $temp_dir;
  }
  cd $temp_dir

  try {
    safe-exec [task _get $"($task).($attribute)"] | save attribute.md
    ^$env.EDITOR attribute.md
    safe-exec [task $task modify $"($attribute):(open attribute.md | str trim)"]
  } catch {|err|
    do $cleanup
    error make $err
  }

  do $cleanup
}

def safe-exec [$args: list<string>]: nothing -> string {
  do { ^$args }
  | complete
  | match $in {
    {exit_code: 0} => { $in.stdout }
    {exit_code: $x} => {
      let result = $in
      error make {
        msg: $"Exited with code ($x): ($args | str join ' ')\n\n($result | table)"
      }
    }
  }
}

def get-template [now: datetime]: nothing -> record {
  let template_file_path = $"($STATE_DIR)/($TEMPLATE_FILE)"
  if ($template_file_path | path exists) {
    let raw_template = open $template_file_path
    # convert expiration string to a datetime
    $raw_template | update add.expire ($raw_template.add.expire | into datetime)
  } else {
    {
      add: {
        expire: $now
        args: []
      }
    }
  }
}

def not-implemented []: nothing -> record {
  { msg: "Work in progress. Not implemented yet." }
}
