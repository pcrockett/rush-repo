#!/usr/bin/env nu

# Query Kagi's FastGPT
def main [
    query: string  # Query to send to FastGPT
    --verbose(-v)  # Display meta information at the end
] {

    # <https://help.kagi.com/kagi/api/fastgpt.html#examples>
    let response = (
        http post
            --content-type "application/json"
            --headers ["Authorization" $"Bot ($env.KGPT_TOKEN)"]
            "https://kagi.com/api/v0/fastgpt"
            {
                query: $query
                cache: true
            }
    )

    let data = $response.data

    print $data.output

    if ($data.references | length) > 0 {
        print "References:\n"
        $data.references | enumerate | each {|it|
            print $"($it.index + 1). [($it.item.title)]\(($it.item.url))"
        }
    }

    if $verbose {
        print ""
        print "---"
        print ""
        print $"* **Tokens used:** _($data.tokens)_"
        print $"* **API balance:** _$($response.meta.api_balance)_"
    }

    ignore
}
